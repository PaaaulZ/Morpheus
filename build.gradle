buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'java'
apply plugin: 'idea'

project.ext {
    major = "4"
    minor = "0"
    if (System.getenv("CI_JOB_ID") != null) {
        build = System.getenv("CI_JOB_ID")
    } else {
        build = "-custom"
    }
    mcVersion = "1.13.2"
}
version = "${project.ext.mcVersion}-${project.ext.major}.${project.minor}.${project.build}"
group = "net.quetzi.morpheus"
archivesBaseName = "Morpheus"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "${project.ext.mcVersion}-${project.version}"
    mappings channel: 'snapshot', version: '20180921-1.13'
    // replacing stuff in the source
    ant.replace(file: 'src/main/java/net/quetzi/morpheus/helpers/References.java', token: '@MAJOR@', value: project.ext.major)
    ant.replace(file: 'src/main/java/net/quetzi/morpheus/helpers/References.java', token: '@MINOR@', value: project.ext.minor)
    ant.replace(file: 'src/main/java/net/quetzi/morpheus/helpers/References.java', token: '@MC_VERSION@', value: project.ext.mcVersion)
    ant.replace(file: 'src/main/java/net/quetzi/morpheus/helpers/References.java', token: '@BUILD_NUMBER@', value: project.ext.build)
    runs {
        client = {
            // recommended logging data for a userdev environment
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
            // recommended logging level for the console
            properties 'forge.logging.console.level': 'debug'
            workingDirectory project.file('run').canonicalPath
            source sourceSets.main
        }
        server = {
            // recommended logging data for a userdev environment
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
            // recommended logging level for the console
            properties 'forge.logging.console.level': 'debug'
            workingDirectory project.file('run').canonicalPath
            source sourceSets.main
        }
    }
}

task libJar(type: Jar) {
    from(sourceSets.main.java) {
        include "net/quetzi/morpheus/api/*"
    }
    classifier = 'api'
}

artifacts {
    archives libJar
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.13.2-25.0.13'
}

jar {
    manifest {
        attributes(["Specification-Title": "Morpheus",
                    "Specification-Vendor": "Quetzi",
                    "Specification-Version": "24.0", // We are version 1 of the modlauncher specification
                    "Implementation-Title": project.name,
                    "Implementation-Version": "${project.version}",
                    "Implementation-Vendor" :"Quetzi",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property 'version', project.version

    from(sourceSets.main.resources.srcDirs) {
        include 'META_INF/mods.toml'

        // replace version and mcversion
        expand 'version':project.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'META_INF/mods.toml'
    }
}